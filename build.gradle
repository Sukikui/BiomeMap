import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'checkstyle'
    id "com.github.spotbugs" version "6.4.2"
    id 'com.gradleup.shadow' version '9.2.2'
    id 'java'
}

group = "fr.sukikui.biomemap"
version = "0.1.0"

def paperApiVersion = '1.21.8-R0.1-SNAPSHOT'
def hyphenIndex = paperApiVersion.indexOf('-')
def minecraftVersion = hyphenIndex > 0 ? paperApiVersion.substring(0, hyphenIndex) : paperApiVersion
def archiveVersionLabel = "${version}+mc${minecraftVersion}"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
        content {
            includeModule("io.papermc.paper", "paper-api")
            includeModule("io.papermc", "paperlib")
            includeModule("net.md-5", "bungeecord-chat")
            includeGroup("io.papermc.adventure")
        }
    }

    maven {
        name = 'minecraft'
        url = 'https://libraries.minecraft.net'
        content {
            includeModule("com.mojang", "brigadier")
        }
    }

    mavenCentral()
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${paperApiVersion}"
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.9.6'
    implementation 'io.papermc:paperlib:1.0.8'
    implementation 'com.google.code.gson:gson:2.13.2'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.14.0'
    testCompileOnly 'com.github.spotbugs:spotbugs-annotations:4.9.6'
    testImplementation "io.papermc.paper:paper-api:${paperApiVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.0'
}

test {
    useJUnitPlatform()
}

processResources {
    filesMatching("**/plugin.yml") {
        expand(
                NAME: rootProject.name,
                VERSION: version,
                PACKAGE: rootProject.group.toString(),
                AUTHOR: "Sukikui",
                DESCRIPTION: "BiomeMap generates a lightweight JSON file mapping each world region to its dominant biome",
                API_VERSION: minecraftVersion
        )
    }
}

checkstyle {
    toolVersion = '12.0.1'
    maxWarnings = 0
}

configurations.checkstyle {
    resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
        select("com.google.guava:guava:23.0")
    }
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required = false
        html.required = true
    }
}

tasks.withType(SpotBugsTask).configureEach {
    reports.create("html") {
        required = true
    }
    reports.create("xml") {
        required = false
    }
}

shadowJar {
    archiveBaseName.set(rootProject.name)
    archiveVersion.set(archiveVersionLabel)
    archiveClassifier.set('')
    relocate 'io.papermc.lib', 'shadow.io.papermc.paperlib'
    minimize()
}

// Disable jar and replace with shadowJar
jar.enabled = false
assemble.dependsOn(shadowJar)

tasks.register('printProjectName') {
    doLast {
        println rootProject.name
    }
}

tasks.register('release') {
    dependsOn build

    doLast {
        if (!version.endsWith("-SNAPSHOT")) {
            // Rename final JAR to trim off version information
            shadowJar.archiveFile.get().getAsFile()
                    .renameTo(layout.buildDirectory.get().toString() + File.separator + 'libs' + File.separator
                            + "${rootProject.name}-${archiveVersionLabel}.jar")
        }
    }
}
